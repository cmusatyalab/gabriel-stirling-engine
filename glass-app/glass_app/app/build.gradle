apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "edu.cmu.cs.sterling"
        minSdkVersion 27
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField("String", "GABRIEL_HOST", properties.getProperty('gabrielHost'))
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    protobuf {
        protoc {
            artifact = 'com.google.protobuf:protoc:3.8.0'
        }
        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    java {
                        option "lite"
                    }
                    python {
                        // This gradle file is located in <REPOSITORY ROOT>/android-client/app
                        // outputSubDir is relative to <REPOSITORY ROOT>/android-client/app/build/generated/source/proto/debug
                        outputSubDir = '../server'
                    }
                }
            }
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation 'com.example.glass.ui:gesture-lib-sample:0.1.0-SNAPSHOT'
    implementation 'edu.cmu.cs.gabriel:client:2.3.0'
    implementation 'edu.cmu.cs.gabriel:protocol:2.0.1'
    implementation 'edu.cmu.cs.gabriel:camera:2.4.0'

    def camerax_version = "1.0.0-rc03"
    // CameraX core library using camera2 implementation
    //noinspection GradleDependency
    implementation "androidx.camera:camera-camera2:$camerax_version"
    // CameraX Lifecycle Library
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    // CameraX View class
    implementation "androidx.camera:camera-view:1.0.0-alpha22"
}